plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.17'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.openapi.generator' version '5.3.1'
}

group = 'com.rabobank'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation('org.springframework.boot:spring-boot-starter-validation')

    // Development Tools
    implementation 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'com.jayway.jsonpath:json-path:2.6.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/static/openapi.yaml".toString()
    outputDir = "${buildDir}/generated-sources".toString()
    configOptions = [
            dateLibrary          : "java8",
            library              : "spring-boot",
            interfaceOnly        : "true",
            skipDefaultInterface : "true",
            performBeanValidation: "true",
            useOptional          : "true",
            useTags              : "true",
            unhandledException   : "true",
            sourceFolder         : "",
            useSpringController  : "false",
            booleanGetterPrefix  : ""
    ]
}

tasks.compileJava.dependsOn tasks.openApiGenerate

test {
    useJUnitPlatform()
}